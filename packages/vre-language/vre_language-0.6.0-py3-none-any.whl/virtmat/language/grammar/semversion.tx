/* semantic versioning https://semver.org */

// parsing using a regex is somewhat slower than using textx grammar

/*
SemVer:
  /(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?/
;
*/

/* https://semver.org/#backusnaur-form-grammar-for-valid-semver-versions */

SemVer:
  SemVerCore ( '-' SemVerPreRelease )? ( '+' SemVerBuild )?
;

SemVerCore:
  SemVerNumericID '.' SemVerNumericID '.' SemVerNumericID
;

SemVerPreRelease:
  SemVerPreReleaseID+['.']
;

SemVerPreReleaseID:
  SemVerAlphaNumericID | SemVerNumericID
;

SemVerBuild:
  SemVerBuildID+['.']
;

SemVerBuildID:
  SemVerAlphaNumericID | SemVerDigit+
;

SemVerAlphaNumericID:
  ( SemVerNonDigit | ( SemVerIDCharacter+ SemVerNonDigit ) ) SemVerIDCharacter*
;

SemVerNumericID:
  SemVerDigit+
;

SemVerIDCharacter:
  SemVerDigit | SemVerNonDigit
;

SemVerNonDigit:
  SemVerLetter | '-'
;

SemVerLetter:
  /[a-zA-Z]/
;

SemVerDigit:
  /[0-9]/
;
